module extraction
imports
  libstratego-lib
  org/sugarj/languages/Sdf2
  org/sugarj/languages/Stratego
  org/sugarj/languages/Sugar

//Accept Rules
rules 
  accept-sdf : grammar-elem(x) -> x
  accept-str : transformation-elem(x) -> x
  accept-editor : editor-elem(x) -> x

rules
  // singleton :: a -> [a]
  singleton : x -> [x]

  children : x#(xs) -> xs

  filter-children(s) : x#(xs) -> x#(<map(filter-tree(s));concat> xs)

strategies
  empty = ![]
  
  /*
   * The argument strategy is tried on the nodes of the current AST in
   * a top-down fashion. The result of `extract` is a list of results of
   * applying `s`. The strategy only recurses when `s` fails.
   */
  // extract :: (a -> [b]) -> [b]
  extract(s) =
  	 s < id
       + children ; map(extract(s)); concat
  

  filter-tree(s) =
  	where(s) < filter-children(s); singleton
             + ![]
  
// to SDF
rules
  comp-desugarings-to-sdf :
    Desugarings(ds) ->
    context-free-syntax(syns)
    where
      <map(comp-desugaring-to-sdf); concat> ds => syns;
      <not(?[])> syns

  comp-desugaring-to-sdf :
    PureDesugaring(_) -> []
    
  comp-desugaring-to-sdf :
    InjectDesugaring(name,from,to,attrs) ->
    [prod([from],to,attrs)]
  
  fold-conc-grammars :
    [] -> empty-grammar
  
  fold-conc-grammars :
    [x] -> x
  
  fold-conc-grammars :
    [x, y | z] ->
      <fold-conc-grammars> [conc-grammars(x, y) | z]
  
strategies
  main-sdf = io-wrap(extract-sdf; fold-conc-grammars)
  extract-sdf = map(extract((accept-sdf + (accept-str; comp-desugarings-to-sdf));
                            singleton));
                    concat

// to STR
rules
  comp-desugarings-to-str :
    Desugarings(as) ->
    Strategies(<map(comp-desugaring-to-str)> as)

  comp-desugaring-to-str :
    PureDesugaring(term) ->
    SDefNoArgs("desugar",Call(CallNoArgs(SVar("changed-tree")),[term]))

  comp-desugaring-to-str :
    InjectDesugaring(term,from,to,attrs) ->
    SDefNoArgs("desugar",Call(CallNoArgs(SVar("changed-tree")),[term]))

strategies
  mainSTR = io-wrap(extract-str)
	extract-str = map(extract(accept-str;
                            try(comp-desugarings-to-str);
                            singleton));
                    concat

strategies
  mainEDITOR = io-wrap(extract-editor)
  extract-editor = map(extract(accept-editor;
                               singleton));
                   concat
